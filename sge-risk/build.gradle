plugins {
    id 'java'
    id 'idea'
    id "maven-publish"
    id 'com.github.johnrengelman.shadow' version '5.2.0'
    id "com.jfrog.bintray" version "1.8.5"
}

group 'at.ac.tuwien.ifs.sge'
version '1.0.2'

sourceCompatibility = 1.11

repositories {
    jcenter()
}

dependencies {
    compile group: 'org.jgrapht', name: 'jgrapht-core', version: '1.3.0'
    compile group: 'com.google.guava', name: 'guava', version: '28.0-jre'
    compile group: 'org.yaml', name: 'snakeyaml', version: '1.24'
    compile group: 'info.picocli', name: 'picocli', version: '3.9.6'
    compile group: 'at.ac.tuwien.ifs.sge', name: 'sge', version: '1.0.1'
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile 'com.pholser:junit-quickcheck-core:0.9'
    testCompile 'com.pholser:junit-quickcheck-generators:0.9'
}

jar {
    manifest {
        attributes 'Sge-Type': 'game'
        attributes 'Game-Class': 'at.ac.tuwien.ifs.sge.game.risk.board.Risk'
        attributes 'Main-Class': 'at.ac.tuwien.ifs.sge.engine.risk.RiskMapGenerator'
    }
}

shadowJar {
    archiveClassifier.set('exe');
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier.set('sources')
    from sourceSets.main.allSource
}

javadoc {
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier.set('javadoc')
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

publishing {
    publications {
        mavenPublication(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            pom {
                name = project.getName()
                description = "A clone of the game \"Risk\" implemented in sge."
                licenses {
                    license {
                        name = "GNU Affero General Public License version 3"
                        url = "https://opensource.org/licenses/agpl-v3.html"
                        distribution = "repo"
                    }
                }
                developers {
                    developer {
                        id = "entze"
                        name = "Lukas Grassauer"
                        email = "entze@grassauer.eu"
                    }
                }

                scm {
                    url = "https://gitlab.com/Entze/sge-risk"
                    connection = "https://gitlab.com/Entze/sge-risk.git"
                    developerConnection = "git@gitlab.com:Entze/sge-risk.git"
                }
            }
            repositories {
                maven {
                    url = "https://jcenter.bintray.com/at/ac/tuwien/ifs/sge/sge-risk/"
                }
            }
        }
    }
}
